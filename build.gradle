plugins {
    id 'groovy'
    id 'maven-publish'
}

group 'org.m2ci.msp'
version '0.6-SNAPSHOT'
description 'Articulatory data ToolKit'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compileOnly localGroovy()
    compile group: 'com.google.guava', name: 'guava', version: '25.1-jre'
    compile group: 'org.ejml', name: 'ejml-simple', version: '0.34'
    compile group: 'com.xlson.groovycsv', name: 'groovycsv', version: '1.3'
    testCompile localGroovy()
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.10.0'
    testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
}

// only use the groovy compiler
sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ["src/main/java", "src/main/groovy"] }
    }
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            pom {
                name = project.name
                description = project.description
                organization {
                    name = 'MMCI'
                    url = 'http://m2ci.org/'
                }
                inceptionYear = '2011'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0 or later'
                        url = 'https://www.gnu.org/licenses/gpl.txt'
                    }
                }
                developers {
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                    developer {
                        id = 'ahewer'
                        name = 'Alexander Hewer'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/m2ci-msp/artk/issues'
                }
                scm {
                    url = 'https://github.com/m2ci-msp/artk'
                    developerConnection = 'scm:git:git@github.com:m2ci-msp/artk.git'
                }
                distributionManagement {
                    downloadUrl = 'https://api.bintray.com/maven/m2ci-msp/maven/artk'
                }
            }
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/m2ci-msp/maven/artk'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }

    }
}
